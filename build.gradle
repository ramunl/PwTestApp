// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.3.21'
    ext.daggerVersion = '2.17'
    ext.timberVersion = '4.7.1'
    ext.rxJavaVersion = '2.1.12'
    ext.rxAndroidVersion = '2.1.0'
    ext.rxKotlinVersion = '2.2.0'
    ext.moxyVersion = '1.5.5'
    ext.loggingInterceptorVersion = '3.8.0'
    ext.retrofitVersion = '2.5.0'

    repositories {
        google()
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/'}
        maven { url 'https://maven.google.com' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    if (it.name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }

    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion 28
        if (this.name == 'app') {
            android.defaultConfig.applicationId = "ru.pwtest.pwapp"
        }
        defaultConfig {
            minSdkVersion 19
            targetSdkVersion 28
            versionCode 2
            versionName "1.1"
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        kapt {
            correctErrorTypes = true
        }
    }

    dependencies {
        implementation fileTree(include: ['*.jar'], dir: 'libs')
        //kotlin support
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        //dagger
        implementation "com.google.dagger:dagger:$daggerVersion"
        implementation "com.google.dagger:dagger-android:$daggerVersion"
        implementation "com.google.dagger:dagger-android-support:$daggerVersion"
        kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
        kapt "com.google.dagger:dagger-compiler:$daggerVersion"
        //moxy
        implementation "com.arello-mobile:moxy:$moxyVersion"
        implementation "com.arello-mobile:moxy-app-compat:$moxyVersion"
        kapt "com.arello-mobile:moxy-compiler:$moxyVersion"
        //Rx
        implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
        implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
        implementation "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        //logger
        implementation "com.jakewharton.timber:timber:$timberVersion"
        implementation "com.squareup.okhttp3:okhttp:$loggingInterceptorVersion"
        implementation "com.squareup.okhttp3:logging-interceptor:$loggingInterceptorVersion"

        implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
